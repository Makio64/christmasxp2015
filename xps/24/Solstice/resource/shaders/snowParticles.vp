uniform float uTime;
uniform float uParticleRadius;
uniform vec3 uBounds;
uniform vec3 uWindDir;

varying float vTime;
varying float vDistToCamera;
varying vec3 vPosWorld;

float rand(vec2 co){
    return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
}

float wrapf(float val, float minVal, float maxVal)
{
  val -= minVal;

  float delta = maxVal - minVal;
  if ( delta < 0.0001 ) return val;

  return val - (delta* floor(val/delta)) + minVal;
}

void main() {

	vec3 pos = position;

	float rand0 = rand(pos.xy* 50.0);
	float rand1 = rand(pos.yz*1000.3);
	float rand2 = rand(pos.xz*3300.3);
	vec3 dir = uWindDir;//vec3(-1.5,-1.0,0.0);
	vec3 dirOpp = vec3(-dir.y, dir.x, 0.0);

	float time = uTime + rand0 * 500.0;
	pos += dir * time * mix( 0.3, 6.0, rand0 );
	pos += dirOpp * sin(position.x + time * mix(0.1, 4.0, rand0)) * mix( 0.1, 0.6, rand2 );

	vec3 bounds = uBounds;
	pos.xyz = vec3( 
		wrapf( pos.x, -bounds.x, bounds.x), 
		wrapf( pos.y, -bounds.y, bounds.y),
		wrapf( pos.z, -bounds.z, bounds.z) );

	vec3 posWorld = (modelMatrix * vec4(pos,1.0)).xyz;
	vec3 vecToCamera = cameraPosition - posWorld;
	float distToCamera = length(vecToCamera);

	//float fogCoeff = clamp(distToCamera*0.003,0.0,1.0);
	float sizeCoeff = sin( time * mix( 0.5, 3.0, rand2 ) + rand0 ) * 0.5 + 0.5;
	float fogCoeff = (1.0-smoothstep( 10.0, 80.0, distToCamera ));
	gl_PointSize = fogCoeff * uParticleRadius * sizeCoeff * mix( 0.75, 1.0, rand1 ) ;
	
	vTime = uTime;
	vPosWorld = posWorld;
	vDistToCamera = distToCamera;

  	gl_Position = projectionMatrix *
                modelViewMatrix *
                vec4(pos,1.0);
}